plugins {
	id 'java-library'
	id 'groovy'
	id 'maven-publish'
	id 'com.jfrog.bintray' version '1.7.3'
}

group = 'dev.kkorolyov'
version = '3.0'
description = "Java library providing for persistence of Java objects in a SQL database"

repositories {
	jcenter()
	maven { url 'https://dl.bintray.com/kkorolyov/java' }
}

dependencies {
	compileOnly 'dev.kkorolyov:simple-logs:3.+'

	implementation(
			'dev.kkorolyov:simple-funcs:1.+',
			'dev.kkorolyov:simple-graphs:1.+'
	)
	testImplementation(
			'org.spockframework:spock-core:1.1-groovy-2.4',
			'cglib:cglib-nodep:3.+',
			'org.objenesis:objenesis:1.+',

			'org.xerial:sqlite-jdbc:3.+',
			'org.postgresql:postgresql:42.+',
			'mysql:mysql-connector-java:6.+',

			'dev.kkorolyov:simple-logs:3.+',
			'dev.kkorolyov:simple-props:4.+',
			'dev.kkorolyov:simple-specs:1.+'
	)
}

test {
	filter.includeTestsMatching '*Spec'
}
task integration(type: Test) {
	group = 'verification'
	description = "Runs integration tests."

	filter.includeTestsMatching '*Int'

	build.dependsOn check
}
task performance(type: Test) {
	group = 'verification'
	description = """Runs performance tests."""

	filter.includeTestsMatching '*Perf'

	build.dependsOn check
}

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allJava
}
task javadocJar(type: Jar) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}
publishing {
	publications {
		Pub(MavenPublication) {
			from components.java
			artifact sourcesJar
			artifact javadocJar
		}
	}
}
bintray {
	user = System.getenv('BINTRAY_USER')
	key = System.getenv('BINTRAY_KEY')
	publications = ['Pub']
	pkg {
		repo = 'java'
		name = project.name
		licenses = ['BSD 3-Clause']
		vcsUrl = 'https://github.com/kkorolyov/SQLOb.git'
		version {
			name = project.version
		}
	}
}

