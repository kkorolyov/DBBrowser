plugins {
	id 'java'
}

group = 'dev.kkorolyov'
version = gitVersion()
description = """Java library providing for persistence of Java objects in a SQL database"""

javadoc {
	destinationDir = new File("docs")
}
jar {
	baseName = baseName.replaceAll('(?<=[a-z])([A-Z])(?=[a-z])', '-$1').toLowerCase()
}

repositories {
	jcenter()
	maven {
		url 'https://jitpack.io'
	}
}

dependencies {
	compileOnly 'com.github.kkorolyov:SimpleLogs:master-SNAPSHOT'
	testCompile 'org.spockframework:spock-core:1.1-groovy-2.4-rc-4',
			'net.bytebuddy:byte-buddy:1.6.13',
			'org.xerial:sqlite-jdbc:3.16.1',
			'org.postgresql:postgresql:42.0.0',
			'mysql:mysql-connector-java:6.0.6',
			'com.github.kkorolyov:SimpleLogs:-SNAPSHOT',
			'com.github.kkorolyov:SimpleProps:-SNAPSHOT'
}

def gitVersion() {
	def stdout = new ByteArrayOutputStream()
	exec {
		commandLine 'git', 'describe'
		standardOutput = stdout
	}
	stdout = stdout.toString().trim()
	return stdout.replaceAll('-.*', "+${stdout.find('(?<=-)[0-9]+')}")
}
